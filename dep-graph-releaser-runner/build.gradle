buildscript {
    ext.publish_description = 'Contains different runners, they are the entry point for users.'
}

dependencies {
    compile prefixedProject('maven')
    compile prefixedProject('serialization')
    compile prefixedProject('gui')
    compile prefixedProject('manipulation')

    runtimeOnly "org.jetbrains.kotlin:kotlin-runtime:$kotlin_version"
    testCompile prefixedProject('maven').sourceSets.test.output
}

task dist {
    def outputDir = "${project.buildDir}/dist"
    def bat = file("src/main/produce.bat")
    outputs.dir outputDir
    inputs.files bat
    inputs.files jar.outputs.files
    inputs.files sourceSets.main.runtimeClasspath

    doLast {
        copy {
            from bat
            into outputDir
        }
        copy {
            from sourceSets.main.runtimeClasspath.asFileTree.findAll { it.name.endsWith(".jar") }
            from jar.outputs.files
            into "$outputDir/lib"
        }
    }
}

task distZip(type: Zip) {
    def outputDir = file("${project.buildDir}/distZip")
    inputs.files dist
    outputs.dir outputDir
    from dist
    destinationDir outputDir
}


def htmlDir = file("${rootProject.buildDir}/html")
def jsonFile = file("$htmlDir/release.json")

task json {
    inputs.property('g', findProperty('g'))
    inputs.property('a', findProperty('a'))
    inputs.property('dr', findProperty('dr') ?: '^$')
    inputs.property('mpoff', findProperty('mpoff') ?: 'on')
    inputs.files dist
    outputs.file jsonFile
    doLast {
        def groupId = findProperty('g')
        def artifactId = findProperty('a')
        if (groupId == null) {
            throw new IllegalArgumentException("you need to specify the groupId with -Pg")
        }
        if (artifactId == null) {
            throw new IllegalArgumentException("you need to specify the artifactId with -Pa")
        }
        javaexec {
            workingDir = rootProject.projectDir
            classpath = fileTree("${project.buildDir}/dist/lib/")
            main = 'ch.loewenfels.depgraph.runner.Main'

            def dr = findProperty('dr') != null ? ["-dr=${findProperty('dr')}"] : []
            def mpOff = findProperty('mpoff') != null ? ['-mpoff'] : []
            args = ['json', groupId, artifactId, "${rootProject.projectDir}/repos", jsonFile.absolutePath] + dr + mpOff
        }
    }
}

task copyResources {
    inputs.files dist
    outputs.dir htmlDir
    doLast {
        mkdir(htmlDir)
        javaexec {
            workingDir = rootProject.projectDir
            classpath = fileTree("${project.buildDir}/dist/lib/")
            main = 'ch.loewenfels.depgraph.runner.Main'
            args = ['html', htmlDir.absolutePath]
        }
    }
}

task cleanHtmlDir(type: Delete){
    delete htmlDir
}

task html {
    dependsOn json
    dependsOn copyResources
    copyResources.mustRunAfter(json)
}

task server {
    doLast {
        SimpleHttpFileServerFactory factory = new SimpleHttpFileServerFactory()
        factory.start(file("$rootProject.buildDir/html"), 8085)
        println("Server Started on 8085 CTRL+C to kill it")
        java.lang.Thread.sleep(Long.MAX_VALUE)
    }
}

task resourcesJar(type: Jar) {
    dependsOn cleanHtmlDir
    dependsOn copyResources
    copyResources.mustRunAfter(cleanHtmlDir)
    from htmlDir
    from "$projectDir/src/jenkins.pom"
    classifier 'resources'
}

ext.publish_artifacts += [resourcesJar, distZip]
