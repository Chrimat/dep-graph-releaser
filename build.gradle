version = "0.1.0-SNAPSHOT"

buildscript {
    ext {
        ghPages_version = version
        kotlin_version = '1.2.21'
        pom_explorer_version = '1.3.1'
        moshi_version = '1.5.0'
        kotlin_html_version = '0.6.8'

        atrium_version = '0.6.0'
        mockito_kotlin_version = '1.5.0'
        dokka_version = '0.9.14'
        dokka_sourceMapping = 'tree/master'

        //spek.gradle
        jacoco_tool_version = '0.7.9'
        spek_version = '1.1.5'
        junit_version = '1.0.2'
        tutteli_spek_extensions = '0.2.0'

        //publish.gradle
        bintray_version = '1.7.3'
        publish_pkg_name = project.name
        github_url= "https://github.com/loewenfels/${project.name}"
        publish_vcsUrl = "${github_url}.git"

        //gh-pages.gradle
        docProjects = subprojects

        //jacoco-multi-project.gradle
        projectsWithoutSources = [] //currently all have sources
        jacocoMulti = [
            sourceProjects: subprojects - project(':atrium'),
            jacocoProjects: subprojects.findAll{
                it.name != "${rootProject.name}-maven-api" &&
                it.name != "atrium"
            } - projectsWithoutSources
        ]

        ownProject = { String name -> project(":${rootProject.name}-$name") }
        srcKotlin = 'src/main/kotlin'
        srcOwnProject = { String name -> "${rootProject.projectDir}/${rootProject.name}-$name/${ext.srcKotlin}" }
    }

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

repositories {
    mavenCentral()
}

subprojects {
    it.version = rootProject.version

    repositories {
        mavenCentral()
        jcenter()
        maven { url  "https://dl.bintray.com/robstoll/copies" }
        maven { url  "https://dl.bintray.com/robstoll/tutteli-jars" }
    }

    apply plugin: 'kotlin'

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    }
}

def publishedProjects = subprojects - project(':atrium')
configure(publishedProjects) {
    it.ext.publish_artifacts = []
}

def projectsWithSources = publishedProjects - projectsWithoutSources
configure(projectsWithSources) {
    apply plugin: 'org.jetbrains.dokka'

    dokka {
        outputFormat = 'html'
        outputDirectory = "$buildDir/kdoc"
        linkMapping {
            dir = "$srcKotlin"
            url = "$github_url/${dokka_sourceMapping}/${project.name}/$srcKotlin"
            suffix = '#L'
        }
        linkMapping {
            dir = srcOwnProject('api')
            url = "$github_url/${dokka_sourceMapping}/${project.name}-api/$srcKotlin"
            suffix = '#L'
        }

        sourceDirs = files(srcKotlin)
    }

    task sourceJar(type: Jar) {
        from project.sourceSets.main.allSource
        classifier 'sources'
    }

    task javadocJar(type: Jar, dependsOn: dokka) {
        from dokka.outputDirectory
        classifier = 'javadoc'
    }

    it.ext.publish_artifacts += [sourceJar, javadocJar]
}

// has to be after the above block which define `it.ext.publish_artifacts`
configure(publishedProjects) { subproject ->
    subproject.ext {
        publish_artifactId = subproject.name
    }
    afterEvaluate {
        apply from: "${rootProject.projectDir}/gradle/scripts/publish.gradle", to: subproject
    }
}

configure(jacocoMulti.jacocoProjects) {
    apply from: "${rootProject.projectDir}/gradle/scripts/spek.gradle"

    dependencies {
        testCompile project(':atrium')
        testCompile "ch.tutteli:tutteli-spek-extensions:$tutteli_spek_extensions"
    }
}


apply from: 'gradle/scripts/jacoco-multi-project.gradle'
apply from: 'gradle/scripts/gh-pages.gradle'
